#version 450
layout(local_size_x = 1, local_size_y = 1) in;
layout(rgba32f, binding = 0) uniform image2D img_output;

/*
    This code is based on raytracing in one weekend (https://raytracing.github.io/books/RayTracingInOneWeekend.html)
*/

// Constants
// See glsl 4.40 spec chapter 4.7.1 for info on infinity
// https://www.khronos.org/registry/OpenGL/specs/gl/GLSLangSpec.4.40.pdf
const int MAT_LAMBERTIAN = 0;
const int MAT_METAL = 1;
const int MAT_DIELECTRIC = 2;

const float infinity = 0.001 / 0;
const float pi = 3.14159265358; // 3.1415926535897932385


float DegreesToRadians(float deg) {
    return deg * pi / 180.0;
}
bool IsNearZero(vec3 v) {
    const float s = 0.000001;
    return (abs(v.x) < s) && (abs(v.y) < s) && (abs(v.z) < s);
}

float Rand(float co) { return fract(sin(co*(91.3458)) * 47453.5453); }
float Rand(vec2 co){ return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453); }
float Rand(vec3 co){ return Rand(co.xy+Rand(co.z)); }
float Rand(vec2 co, float min, float max) {
    return min + (max - min) * Rand(co);
}
vec3 RandVec3(vec2 co) {
    float x = Rand(co);
    float y = Rand(vec2(co.x + x, co.y + x));
    float z = Rand(vec2(co.x + y, co.y + y));
    return vec3(x, y, z);
}
vec3 RandVec3(vec2 co, float min, float max) {
    float x = Rand(co, min, max);
    float y = Rand(vec2(co.x + x, co.y + x), min, max);
    float z = Rand(vec2(co.x + y, co.y + y), min, max);
    return vec3(x, y, z);
}
vec3 RandUnitVector(vec2 co) {
    return normalize(RandVec3(co, -1, 1));
}
vec3 RandInHemisphere(vec2 co, vec3 normal) {
    vec3 in_unit_sphere = RandVec3(co, -1, 1);
    while (dot(in_unit_sphere, in_unit_sphere) >= 1) {
        in_unit_sphere = RandVec3(co, -1, 1);
    }
    if (dot(in_unit_sphere, normal) > 0.0) // In the same hemisphere as the normal
        return in_unit_sphere;
    else
        return -in_unit_sphere;
}

struct Ray {
    vec3 origin;
    vec3 direction;
};
Ray CreateRay(vec3 origin, vec3 direction);
vec3 RayAt(Ray r, float t);
vec3 RayColor(Ray r);

struct HitRecord {
    vec3 point;
    vec3 normal;
    float t;
    int index; 
    bool front_face;
};

struct Camera {
    int image_width;
    int image_height;

    vec3 horizontal;
    vec3 vertical;

    vec3 lower_left_corner;
    vec3 origin;

    int samples_per_pixel;
    int max_bounce;
};
uniform Camera camera;
Ray CameraGetRay(Camera camera, float u, float v);


// TODO: 6.13. An Abstraction for Hittable Objects
//       abstraction for hittable on GPU can use 
//       "location aliasing" (https://www.khronos.org/opengl/wiki/Layout_Qualifier_(GLSL)#Interface_components)
//       Pad all primitives with the same byte size and specify type using a
//       enum (int). Using the enum we can find out which alias we are using and bring meaning to the struct 
//       data and call the correct method. The negative here is the padding meaning we will waste bytes
//       We can also just keep adding primitives which will bloat our code, but save bytes. It's worth
//       noting that this is not needed if we go for voxels 

struct Sphere {
    vec3 center; 
    float radius;
    // float as all data is in the same vbo,
    // materials should be limited anyways so this should not 
    // cause too much problems (famous last words?)
    float material_index; 

    // TODO: find use for these floats. Could just include albedo since
    //       we need 3 floats anyways. One problem here is that using a vec3
    //       does not work (link below talks about vendors not implementing vec3)
    //       in this context properly).
    // According to link below this should not be needed ...
    float padding1;
    float padding2;
    float padding3;
};                    
// Read about std here: https://www.khronos.org/opengl/wiki/Interface_Block_(GLSL)#Memory_layout
layout (std430, binding = 0) buffer SphereBuffer {
    Sphere spheres[];
};
vec3 SphereAlbedo(int index);
bool SpheresHit(Ray r, float t_min, float t_max, out HitRecord hit); 
bool SphereHit(int index, Ray r, float t_min, float t_max, out HitRecord hit);

struct Material {
    int type;
    int attribute_index;
    int albedo_index;
};
layout (std430, binding = 1) buffer MaterialBuffer {
    Material materials[];
};
bool ScatterLambertian(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered);
bool ScatterMetal(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered);
bool ScatterDielectric(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered);

struct Albedo {
    vec3 color;
};
layout (std430, binding = 2) buffer AlbedoBuffer {
    Albedo albedos[];
};
struct Metal {
    float fuzz;
};
layout (std430, binding = 3) buffer MetalBuffer {
    Metal metal[];
};
struct Dielectric {
    float ir;
};
layout (std430, binding = 4) buffer DielectricBuffer {
    Dielectric dielectric[];
};

void main() {
    ivec2 pixel_coord = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);

    vec3 color = vec3(0.0, 0.0, 0.0);
    for (int i = 0; i < camera.samples_per_pixel; i++) {
        float x = float(pixel_coord.x);
        float y = float(pixel_coord.y);
        float u = (x + Rand(vec2(x + i, y))) / (camera.image_width - 1);
        float v = (y + Rand(vec2(x, y + i))) / (camera.image_height - 1);
        Ray ray = CameraGetRay(camera, u, v);
        color += RayColor(ray);
    }

    float scale = 1.0 / camera.samples_per_pixel;
    color = clamp(color * scale, 0, 1);
    color = sqrt(color);
    imageStore(img_output, pixel_coord, vec4(color, 1.0));
}

Ray CreateRay(vec3 origin, vec3 direction) {
    return Ray(origin, normalize(direction));
}

vec3 RayAt(Ray r, float t) {
    // t * dir + origin
    return fma(vec3(t), r.direction, r.origin);
}

// TODO: This function has way too much branching for glsl ...
vec3 RayColor(Ray r) {
    HitRecord hit;
    Ray current_ray = r;
    vec3 accumulative_attenuation = vec3(1.0);
    int loop_count = 0;
    while (loop_count < camera.max_bounce && SpheresHit(current_ray, 0.001, infinity, hit)) {
        loop_count += 1;

        Ray scattered;
        vec3 attenuation;

        bool result = false;
        int m_index = int(spheres[hit.index].material_index);
        switch (materials[m_index].type) {
        case MAT_LAMBERTIAN: 
            result = ScatterLambertian(hit.index, current_ray, hit, attenuation, scattered);
            break;
        case MAT_METAL: 
            result = ScatterMetal(hit.index, current_ray, hit, attenuation, scattered);
            break;
        case MAT_DIELECTRIC:
            result = ScatterDielectric(hit.index, current_ray, hit, attenuation, scattered);
            break;
        default: 
            return vec3(1.0, 0.0, 0.0); // DEBUG
        }

        if (!result) return vec3(0.0);
        accumulative_attenuation *= attenuation;
        current_ray = scattered;
    }
    if (loop_count == camera.max_bounce) return vec3(0, 0, 0);
    if (loop_count > 0) return accumulative_attenuation;

    float t = 0.5 * (current_ray.direction.y + 1.0);
    vec3 background_color = (1.0 - t) * vec3(1.0, 1.0, 1.0) + t * vec3(0.5, 0.7, 1.0);
    return background_color;
}

Ray CameraGetRay(Camera camera, float u, float v) {
    vec3 ray_dir =  fma(camera.horizontal, vec3(u), camera.lower_left_corner) + fma(vec3(v), camera.vertical, -camera.origin);
    return CreateRay(camera.origin, ray_dir);
}

vec3 SphereAlbedo(int index) {
    int mat_index = int(spheres[index].material_index);
    Material mat = materials[mat_index];
    return albedos[mat.albedo_index].color;
}

bool SpheresHit(Ray r, float t_min, float t_max, out HitRecord hit) {
    HitRecord temp_hit;
    bool has_hit = false;
    float closest = t_max;
    for (int i = 0; i < spheres.length(); i++) {
        if (SphereHit(i, r, t_min, closest, temp_hit)) {
            has_hit = true;
            closest = temp_hit.t;
            hit = temp_hit;
        }
    }

    return has_hit;
} 

bool SphereHit(int index, Ray r, float t_min, float t_max, out HitRecord hit) {
    // calculate vector from sphere to ray origin
    Sphere sphere = spheres[index];
    vec3 oc = r.origin - sphere.center; 
    float a = dot(r.direction, r.direction);
    float half_b = dot(oc, r.direction);
    float c = dot(oc, oc) - sphere.radius * sphere.radius;
    
    float discriminant = half_b * half_b - a * c;
    if (discriminant < 0) {
        return false;
    }

    // optimized abc formula: 
    // https://en.wikipedia.org/wiki/Quadratic_equation#Quadratic_formula_and_its_derivation
    float sqrt_disc = sqrt(discriminant);
    float root = (-half_b - sqrt_disc ) / a;
    if (root < t_min || t_max < root) {
        root = (-half_b + sqrt_disc ) / a; 
        if (root < t_min || t_max < root) {
            return false;
        }
    }

    hit.t = root;
    vec3 point = RayAt(r, root);
    hit.point = point;
    vec3 normal = normalize((point - sphere.center) / sphere.radius);
    hit.front_face = dot(r.direction, normal) < 0; 
    hit.normal = normalize(normal * (1 - (2 * float(!hit.front_face))));
    hit.index = index;
    return true;
}

bool ScatterLambertian(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered) {
    vec3 scatter_dir = hit.normal + RandInHemisphere(hit.point.xy, hit.normal);
    if (IsNearZero(scatter_dir)) scatter_dir = hit.normal;
    scattered = CreateRay(hit.point, scatter_dir);
    attenuation = SphereAlbedo(index);
    return true;
}

bool ScatterMetal(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered) {
    vec3 reflected = reflect(r_in.direction, normalize(hit.normal));
    Material mat = materials[int(spheres[index].material_index)];
    float fuzz = metal[mat.attribute_index].fuzz;
    scattered = CreateRay(hit.point, reflected + fuzz * RandInHemisphere(hit.point.xy, hit.normal));
    attenuation = SphereAlbedo(index);
    return (dot(scattered.direction, hit.normal) > 0);
}

// Sclick Approximation
float reflectance(float cosine, float ref_ratio) {
    float r0 = pow((1 - ref_ratio) / (1 + ref_ratio), 2);
    return fma(pow((1 - cosine), 5), (1 - r0), r0);
}

bool ScatterDielectric(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered) {
    attenuation = vec3(1.0, 1.0, 1.0);
    Material mat = materials[int(spheres[index].material_index)];
    float ir = dielectric[mat.attribute_index].ir;
    float refraction_ratio = ir;
    if(hit.front_face) {
        refraction_ratio = (1.0/ir); 
    } 

    vec3 unit_direction = normalize(r_in.direction);

    float cos_theta = min(dot(-unit_direction, hit.normal), 1.0);
    float sin_theta = sqrt(1.0 - cos_theta * cos_theta);

    vec3 direction;
    bool cannot_refract = refraction_ratio * sin_theta > 1.0;
    bool should_reflect =  reflectance(cos_theta, refraction_ratio) > Rand(hit.point.xy);
    if (cannot_refract || should_reflect) {
        direction = reflect(unit_direction, hit.normal);
    } else {
        direction = refract(unit_direction, hit.normal, refraction_ratio);
    }

    scattered = CreateRay(hit.point, direction);
    return true;
}