#version 450

layout(local_size_x = 32, local_size_y = 32) in;
layout(rgba32f, binding = 0) uniform image2D img_output;
layout(rgba32f, binding = 1) uniform image3D octree;

/*
    This code is based on raytracing in one weekend (https://raytracing.github.io/books/RayTracingInOneWeekend.html)
*/

// Constants
// See glsl 4.40 spec chapter 4.7.1 for info on infinity
// https://www.khronos.org/registry/OpenGL/specs/gl/GLSLangSpec.4.40.pdf
const int MAT_LAMBERTIAN = 0;
const int MAT_METAL = 1;
const int MAT_DIELECTRIC = 2;

const float infinity = 0.001 / 0;
const float pi = 3.14159265358; // 3.1415926535897932385

bool IsNearZero(vec3 v) {
    const float s = 0.000001;
    return (abs(v.x) < s) && (abs(v.y) < s) && (abs(v.z) < s);
}

float Rand(float co) { return fract(sin(co*(91.3458)) * 47453.5453); }
float Rand(vec2 co){ return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453); }
float Rand(vec3 co){ return Rand(co.xy+Rand(co.z)); }
float Rand(vec2 co, float min, float max) {
    return min + (max - min) * Rand(co);
}
vec3 RandVec3(vec2 co) {
    float x = Rand(co);
    float y = Rand(vec2(co.x + x, co.y + x));
    float z = Rand(vec2(co.x + y, co.y + y));
    return vec3(x, y, z);
}
vec3 RandVec3(vec2 co, float min, float max) {
    float x = Rand(co, min, max);
    float y = Rand(vec2(co.x + x, co.y + x), min, max);
    float z = Rand(vec2(co.x + y, co.y + y), min, max);
    return vec3(x, y, z);
}
vec3 RandUnitVector(vec2 co) {
    return normalize(RandVec3(co, -1, 1));
}
vec3 RandInHemisphere(vec2 co, vec3 normal) {
    vec3 in_unit_sphere = RandVec3(co, -1, 1);
    while (dot(in_unit_sphere, in_unit_sphere) >= 1) {
        in_unit_sphere = RandVec3(co, -1, 1);
    }
    if (dot(in_unit_sphere, normal) > 0.0) // In the same hemisphere as the normal
        return in_unit_sphere;
    else
        return -in_unit_sphere;
}

struct Ray {
    vec3 origin;
    vec3 direction;
};
Ray CreateRay(vec3 origin, vec3 direction);
vec3 RayAt(Ray r, float t);
vec3 RayColor(Ray r);

struct HitRecord {
    vec3 point;
    vec3 normal;
    float t;
    int index; 
    bool front_face;
};

struct Camera {
    int image_width;
    int image_height;

    vec3 horizontal;
    vec3 vertical;

    vec3 lower_left_corner;
    vec3 origin;

    int samples_per_pixel;
    int max_bounce;
};
uniform Camera camera;
Ray CameraGetRay(Camera camera, float u, float v);

struct Node {
    int value;
    int type;
};

// Octree cell type
const int EMPTY = 0;
const int PARENT = 1; 
const int LEAF = 2;

// TODO: uniform and data buffer:
const vec3 octree_min = vec3(-2, -2, -2); 
const float octree_scale = 4.0; // world scale

// TODOs for data: 
// convert float to int values
// flatten structure 

// depth 1
// const int octree_max_depth = 1;
// const int octree_cell_count = 1;
// const Node indirect_cells[octree_cell_count * 2][2][2] = Node[octree_cell_count * 2][2][2](
//     // cell 0
//     Node[2][2](                         
//         Node[2](Node(0, EMPTY), Node(0, LEAF)), 
//         Node[2](Node(0, EMPTY), Node(0, EMPTY))
//     ),
//     Node[2][2](                         
//         Node[2](Node(0, EMPTY), Node(0, EMPTY)), 
//         Node[2](Node(0, LEAF), Node(0, EMPTY))
//     )
//     // ------
// );
// const ivec3 indirect_pool_size = ivec3(octree_cell_count * 2, 2, 2); // index max size

// depth 2
// const int octree_max_depth = 2;
// const int octree_cell_count = 2;
// const Node indirect_cells[octree_cell_count * 2][2][2] = Node[octree_cell_count * 2][2][2](
//     // cell 0 (root)
//     Node[2][2](                         
//         Node[2](Node(1, EMPTY), Node(1, PARENT)), 
//         Node[2](Node(1, EMPTY), Node(1, EMPTY))
//     ),
//     Node[2][2](                         
//         Node[2](Node(1, EMPTY), Node(1, EMPTY)), 
//         Node[2](Node(1, PARENT), Node(0, EMPTY))
//     ),
//     // cell 1
//     Node[2][2](                         
//         Node[2](Node(0, EMPTY), Node(0, LEAF)), 
//         Node[2](Node(0, EMPTY), Node(0, EMPTY))
//     ),
//     Node[2][2](                         
//         Node[2](Node(0, EMPTY), Node(0, EMPTY)), 
//         Node[2](Node(3, LEAF), Node(0, EMPTY))
//     )
//     // ------
// );
// const ivec3 indirect_pool_size = ivec3(octree_cell_count * 2, 2, 2);

// depth 5
const int octree_max_depth = 5;
const int octree_cell_count = 5;
const Node indirect_cells[(octree_cell_count * 2) * 2 * 2] = Node[(octree_cell_count * 2) * 2 * 2](
    // cell 0 (root)
    Node(1, EMPTY),  Node(1, PARENT), 
    Node(1, EMPTY),  Node(1, EMPTY),
    Node(1, EMPTY),  Node(1, EMPTY), 
    Node(1, PARENT), Node(0, EMPTY),
    // cell 1
    Node(0, EMPTY),  Node(2, PARENT), 
    Node(0, EMPTY),  Node(0, EMPTY),
    Node(0, EMPTY),  Node(0, EMPTY), 
    Node(2, PARENT), Node(0, EMPTY),
    // cell 2
    Node(0, EMPTY),  Node(3, PARENT), 
    Node(0, EMPTY),  Node(0, EMPTY),
    Node(0, EMPTY),  Node(2, EMPTY), 
    Node(3, PARENT), Node(0, EMPTY),
    // cell 3
    Node(0, EMPTY),  Node(4, PARENT), 
    Node(0, EMPTY),  Node(0, EMPTY),
    Node(0, EMPTY),  Node(2, EMPTY), 
    Node(4, PARENT), Node(0, EMPTY),
     // cell 4
    Node(0, EMPTY), Node(2, LEAF), 
    Node(0, EMPTY), Node(1, LEAF),
    Node(0, EMPTY), Node(3, LEAF), 
    Node(0, LEAF),  Node(0, EMPTY)
    // ------
);
const ivec3 indirect_pool_size = ivec3(octree_cell_count * 2, 2, 2);
Node AccessIndirectCell(ivec3 i) {
    return indirect_cells[i.z + indirect_pool_size.y * (i.y + indirect_pool_size.z * i.x)];
}


// Octree methods
bool OctreeHit(Ray r, float t_min, float t_max, out HitRecord hit);
bool CubeHit(vec3 cmin, float size, Ray r, float t_min, inout float t_max, out HitRecord hit);

struct Sphere {
    vec3 center; 
    float radius;
    // float as all data is in the same vbo,
    // materials should be limited anyways so this should not 
    // cause too much problems (famous last words?)
    float material_index; 

    // TODO: find use for these floats. Could just include albedo sinceempty
    //       we need 3 floats anyways. One problem here is that using a vec3
    //       does not work (link below talks about vendors not implementing vec3)
    //       in this context properly).
    // According to link below this should not be needed ...
    float padding1;
    float padding2;
    float padding3;
};                    
// Read about std here: https://www.khronos.org/opengl/wiki/Interface_Block_(GLSL)#Memory_layout
layout (std430, binding = 0) buffer SphereBuffer {
    Sphere spheres[];
};
vec3 SphereAlbedo(int index);
bool SpheresHit(Ray r, float t_min, float t_max, out HitRecord hit); 
bool SphereHit(int index, Ray r, float t_min, float t_max, out HitRecord hit);

struct Material {
    int type;
    int attribute_index;
    int albedo_index;
};
layout (std430, binding = 1) buffer MaterialBuffer {
    Material materials[];
};
bool ScatterLambertian(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered);
bool ScatterMetal(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered);
bool ScatterDielectric(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered);

struct Albedo {
    vec3 color;
};
layout (std430, binding = 2) buffer AlbedoBuffer {
    Albedo albedos[];
};
struct Metal {
    float fuzz;
};
layout (std430, binding = 3) buffer MetalBuffer {
    Metal metal[];
};
struct Dielectric {
    float ir;
};
layout (std430, binding = 4) buffer DielectricBuffer {
    Dielectric dielectric[];
};

void main() {
    ivec2 pixel_coord = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);

    vec3 color = vec3(0.0, 0.0, 0.0);
    for (int i = 0; i < camera.samples_per_pixel; i++) {
        float x = float(pixel_coord.x);
        float y = float(pixel_coord.y);
        float u = (x + Rand(vec2(x + i, y))) / (camera.image_width - 1);
        float v = (y + Rand(vec2(x, y + i))) / (camera.image_height - 1);
        Ray ray = CameraGetRay(camera, u, v);
        color += RayColor(ray);
    }

    float scale = 1.0 / camera.samples_per_pixel;
    color = clamp(color * scale, 0, 1);
    color = sqrt(color);
    imageStore(img_output, pixel_coord, vec4(color, 1.0));
}

Ray CreateRay(vec3 origin, vec3 direction) {
    return Ray(origin, normalize(direction));
}

vec3 RayAt(Ray r, float t) {
    // instruction for: t * dir + origin
    return fma(vec3(t), r.direction, r.origin);
}

// TODO: This function has way too much branching for glsl ...
vec3 RayColor(Ray r) {
    HitRecord hit;
    Ray current_ray = r;
    vec3 accumulative_attenuation = vec3(1.0);
    int loop_count = 0;

    while (loop_count < camera.max_bounce && OctreeHit(current_ray, 0.001, infinity, hit)){ 
        loop_count += 1;

        Ray scattered;
        vec3 attenuation;

        bool result = false;
        int m_index = int(spheres[hit.index].material_index);
        switch (materials[m_index].type) {
        case MAT_LAMBERTIAN: 
            result = ScatterLambertian(hit.index, current_ray, hit, attenuation, scattered);
            break;
        case MAT_METAL: 
            result = ScatterMetal(hit.index, current_ray, hit, attenuation, scattered);
            break;
        case MAT_DIELECTRIC:
            result = ScatterDielectric(hit.index, current_ray, hit, attenuation, scattered);
            break;
        default: 
            result = false;
            break;
        }
        if (!result) break;
        
        accumulative_attenuation *= attenuation;
        current_ray = scattered;
    }
    if (loop_count > 0) return accumulative_attenuation;

    float t = 0.5 * (current_ray.direction.y + 1.0);
    vec3 background_color = fma(vec3(1.0 - t), vec3(1.0, 1.0, 1.0), t * vec3(0.5, 0.7, 1.0));
    return background_color;
}

Ray CameraGetRay(Camera camera, float u, float v) {
    vec3 ray_dir =  fma(camera.horizontal, vec3(u), camera.lower_left_corner) + fma(vec3(v), camera.vertical, -camera.origin);
    return CreateRay(camera.origin, ray_dir);
}

vec3 SphereAlbedo(int index) {
    int mat_index = int(spheres[index].material_index);
    Material mat = materials[mat_index];
    return albedos[mat.albedo_index].color;
}

float axisMax(vec3 v) {
    return max(max(v.x, v.y), v.z);
}

// TODO: Source: (Ray tracing gems 2) / Peter Shirley
bool CubeHit(vec3 cmin, float size, Ray r, float t_min, inout float t_max, out HitRecord hit) {
    vec3 cmax = cmin + vec3(size);

    vec3 inv_ray_dir = 1 / r.direction;
    vec3 t_lower = (cmin - r.origin) * inv_ray_dir;
    vec3 t_upper = (cmax - r.origin) * inv_ray_dir;

    vec3 t_mins = vec3(min(t_lower, t_upper));
    vec3 t_maxes = vec3(max(t_lower, t_upper));

    float t_cube_min = t_min;
    float t_cube_max = t_max;
    for (int i = 0; i < 3; i++) {
        t_cube_min = max(t_cube_min, t_mins[i]);
        t_cube_max = min(t_cube_max, t_maxes[i]);
    }
    if (t_cube_min > t_cube_max) {
        return false;
    }

    // store the exit t in t_max
    t_max = t_cube_max;
    hit.t = t_cube_min;
    hit.point = RayAt(r, hit.t);
    
    float radius = size * 0.5;
    vec3 center = cmin + vec3(radius);
    
    vec3 normal = (hit.point - center);
    vec3 abs_norm = abs(normal);
    float keep_x = float(abs_norm.x >= abs_norm.y && abs_norm.x >= abs_norm.z);
    float keep_y = float(abs_norm.y > abs_norm.x && abs_norm.y > abs_norm.z);
    float keep_z = float(abs_norm.z > abs_norm.x && abs_norm.z > abs_norm.y); 
    normal = normalize(vec3(normal.x * keep_x, normal.y * keep_y, normal.z * keep_z));
    hit.front_face = dot(r.direction, normal) < 0; 
    hit.normal = normalize(normal * fma(-2, int(!hit.front_face), 1));

    return true;
}

// Does a lookup in the texture octree and returns current cell (pixel)
// Should only be use in OctreeHit
bool treeLookup(
    vec3 voxel_coord,       // point we are doing lookup with in image coordinates
    out float pow_depth,    // depth of current lookup node
    out vec3 grid_uv,        // uv coordinates of node in grid
    out Node node
) {
    vec3 depth_coords = voxel_coord;
    
    node = Node(0, 0);
    pow_depth = 1;
    grid_uv = vec3(0);
   
    for (float i = 0; i < octree_max_depth; i++) {
        pow_depth = pow_depth * 2;
        vec3 point_uv = fract(depth_coords);
        point_uv.x = (node.value + point_uv.x) / octree_cell_count;
        ivec3 point = ivec3(round(fma(point_uv, vec3(indirect_pool_size), vec3(-0.5))));

        vec3 added_uv = (mod(point, 2) / pow_depth);
        grid_uv += added_uv;

        node = AccessIndirectCell(point);
        
        // if cell is not a parent
        bool is_empty = node.type == EMPTY;
        bool is_leaf = node.type == LEAF;
        if (is_empty || is_leaf) { 
            // hit voxel, return true if is leaf
            return is_leaf;
        } 

        depth_coords = depth_coords * 2;
    }
    return false;
}

// Traverse octree and check for hits in leaf nodes
bool OctreeHit(Ray r, float t_min, float t_max, out HitRecord hit) {
    // retrieve octree bound at hit point
    // TODO: file const
    const float min_t_advance = 0.00001;
    float inv_oct_scale = 1 / octree_scale; // TODO: uniform
    vec3 c_min = octree_min;
    float c_scale = octree_scale;
    HitRecord c_hit;

    // record t size of octree relative to ray
    float t_octree_max = t_max;
    bool cube_hit = CubeHit(c_min, c_scale, r, t_min, t_octree_max, c_hit);
    float t_stride = c_hit.t;
    
    // TODO: uniform max iter (30)
    for (int i = 0; i < 30 && cube_hit; i++) { 
        t_stride = t_stride;

        vec3 world_point = RayAt(r, t_stride + min_t_advance);
        vec3 lookup_point = (world_point - octree_min) * inv_oct_scale;
        // TODO: This hack checks explicitly if lookup is not in octree, this should
        //       be handled through other means ...
        if (fract(lookup_point) - lookup_point != vec3(0.0)) { 
            return false;
        }

        float pow_depth; 
        vec3 grid_uv;
        Node node;
        if (treeLookup(lookup_point, pow_depth, grid_uv, node)) {
            // TODO: code should be restructured to not need this branch as it is the 
            //       same code as some code further down
            if (i > 0) {
                c_min = fma(grid_uv, vec3(octree_scale), octree_min);
                c_scale = octree_scale / pow_depth;  
                
                t_min = t_stride - min_t_advance;
                t_stride = t_octree_max;
                cube_hit = CubeHit(c_min, c_scale, r, t_min, t_stride, c_hit);
            }

            hit = c_hit;
            hit.index = node.value;
            return true;
        }

        c_min = fma(grid_uv, vec3(octree_scale), octree_min);
        c_scale = octree_scale / pow_depth;  
        
        t_min = t_stride - min_t_advance;
        t_stride = t_octree_max;
        cube_hit = CubeHit(c_min, c_scale, r, t_min, t_stride, c_hit);
    }
    
    return false;
}

bool SpheresHit(Ray r, float t_min, float t_max, out HitRecord hit) {
    HitRecord temp_hit;
    bool has_hit = false;
    float closest = t_max;
    for (int i = 0; i < spheres.length(); i++) {
        if (SphereHit(i, r, t_min, closest, temp_hit)) {
            has_hit = true;
            closest = temp_hit.t;
            hit = temp_hit;
        }
    }

    return has_hit;
} 

bool SphereHit(int index, Ray r, float t_min, float t_max, out HitRecord hit) {
    // calculate vector from sphere to ray origin
    Sphere sphere = spheres[index];
    vec3 oc = r.origin - sphere.center; 
    float a = dot(r.direction, r.direction);
    float half_b = dot(oc, r.direction);
    float c = dot(oc, oc) - sphere.radius * sphere.radius;
    
    float discriminant = half_b * half_b - a * c;
    if (discriminant < 0) {
        return false;
    }

    // optimized abc formula: 
    // https://en.wikipedia.org/wiki/Quadratic_equation#Quadratic_formula_and_its_derivation
    float sqrt_disc = sqrt(discriminant);
    float root = (-half_b - sqrt_disc ) / a;
    if (root < t_min || t_max < root) {
        root = (-half_b + sqrt_disc ) / a; 
        if (root < t_min || t_max < root) {
            return false;
        }
    }

    hit.t = root;
    vec3 point = RayAt(r, root);
    hit.point = point;
    vec3 normal = normalize((point - sphere.center) / sphere.radius);
    hit.front_face = dot(r.direction, normal) < 0; 
    hit.normal = normalize(normal * fma(-2, float(!hit.front_face), 1));
    hit.index = index;
    return true;
}

bool ScatterLambertian(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered) {
    vec3 scatter_dir = hit.normal + RandInHemisphere(hit.point.xy, hit.normal);
    if (IsNearZero(scatter_dir)) scatter_dir = hit.normal;
    scattered = CreateRay(hit.point, scatter_dir);
    attenuation = SphereAlbedo(index);
    return true;
}

bool ScatterMetal(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered) {
    vec3 reflected = reflect(r_in.direction, normalize(hit.normal));
    Material mat = materials[int(spheres[index].material_index)];
    float fuzz = metal[mat.attribute_index].fuzz;
    scattered = CreateRay(hit.point, reflected + fuzz * RandInHemisphere(hit.point.xy, hit.normal));
    attenuation = SphereAlbedo(index);
    return (dot(scattered.direction, hit.normal) > 0);
}

// Sclick Approximation
float reflectance(float cosine, float ref_ratio) {
    float r0 = pow((1 - ref_ratio) / (1 + ref_ratio), 2);
    return fma(pow((1 - cosine), 5), (1 - r0), r0);
}

bool ScatterDielectric(int index, Ray r_in, HitRecord hit, out vec3 attenuation, out Ray scattered) {
    attenuation = vec3(1.0, 1.0, 1.0);
    Material mat = materials[int(spheres[index].material_index)];
    float ir = dielectric[mat.attribute_index].ir;
    float refraction_ratio = ir;
    if(hit.front_face) {
        refraction_ratio = (1.0/ir); 
    } 

    float cos_theta = min(dot(-r_in.direction, hit.normal), 1.0);
    float sin_theta = sqrt(1.0 - cos_theta * cos_theta);

    vec3 direction;
    bool cannot_refract = refraction_ratio * sin_theta > 1.0;
    bool should_reflect =  reflectance(cos_theta, refraction_ratio) > Rand(hit.point.xy);
    if (cannot_refract || should_reflect) {
        direction = reflect(r_in.direction, hit.normal);
    } else {
        direction = refract(r_in.direction, hit.normal, refraction_ratio);
    }

    scattered = CreateRay(hit.point, direction);
    return true;
}